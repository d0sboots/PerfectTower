:import factory constants

:budget_cap max

:name {script_name(init)}

wakeup()
open.factory()

isopen("factory")

:global double factory_target_amount
:global int factory_target

:local double target_value

; Run the UI, to display the current pending item.
execute("{script_name(ui)}")

; Initial dispatch on entering the factory to determine if we are resuming
; a crafting operation or waiting to launch a new one.
gotoif(wait_loop, {get_raw(factory_target)} == 0.)

; The core factory cycle. We do each loop of factory production within
; budget, which executes within one frame.
;
; This loop handles both calculating recipes via "run_recipes"
; and crafting via "craft". This is done with conditional execution,
; in order to save lines for future possible features.
begin_cycle:

; We have to save this, because the value can change as a result of executing
; later scripts.
target_value = {get_raw(factory_target)}

; All conditional execution in the loop is behind this condition. If it
; is true, then we're here because of `ui`. Otherwise,
; this is a regular crafting iteration.

executesync(if(target_value > 0., "{script_name(produce)}", "{script_name(run_recipes)}"))
stop(if(target_value > 0., "{script_name(produce)}", "{script_name(run_recipes)}"))
executesync(if(\
  target_value > 0.,\
  "{script_name(craft)}",\
  "###badname###"\
))
stop(if(target_value > 0., "{script_name(craft)}", "###badname###"))

; Clear factory_target to indicate that crafting is done, if it is, in fact, done.
factory_target = if({get_raw(factory_target)} > 0., factory_target, 0)
; Re-display the UI when crafting is done.
execute(if(factory_target == 0, "{script_name(ui)}", "###badname###"))

; Clear this (unconditionally), to signal that we're not starting a new crafting
; pass.
factory_target_amount = 0.

; Here we either return to the next iteration of the production loop,
; or stall on this instruction until we need to launch the factory.
wait_loop:
waitframe()
gotoif(\
  if(max(factory_target_amount, {get_raw(factory_target)}) > 0.,\
    begin_cycle, wait_loop\
  ),\
  isopen("factory")\
)

; Remove the UI status so it doesn't clutter the variables when we're outside the
; factory.
gss({status}, "</size>")
